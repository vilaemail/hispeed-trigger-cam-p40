plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def keystorePropertiesFile = rootProject.file("signing/keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
def hasProjectProps = project.hasProperty('KEYSTORE_FILE')

android {
    namespace 'com.hispeedtriggercam.p40'
    compileSdk 36

    defaultConfig {
        applicationId "com.hispeedtriggercam.p40"
        minSdk 29
        targetSdk 36
        versionCode 1
        versionName "1.0.0"
    }

    signingConfigs {
        release {
            if (hasProjectProps) {
                storeFile file(project.property('KEYSTORE_FILE'))
                storePassword project.property('KEYSTORE_PASSWORD')
                keyAlias project.property('KEY_ALIAS')
                keyPassword project.property('KEY_PASSWORD')
            } else if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } else if (System.getenv("KEYSTORE_FILE")) {
                storeFile file(System.getenv("KEYSTORE_FILE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists() || System.getenv("KEYSTORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
    }
}

// Convert patched hwcamerakit.jar (Java bytecode from dex2jar) back to dex format.
// The dex JAR is bundled as an asset and loaded at runtime via ParentLastDexClassLoader,
// bypassing Huawei's shared-library classloader that would shadow our patched classes.
task dexifyPatchedJar {
    def inputJar = rootProject.file("libs/hwcamerakit.jar")
    def assetsDir = file("src/main/assets")
    def outputJar = new File(assetsDir, "hwcamerakit_patched.dex.jar")

    inputs.file inputJar
    outputs.file outputJar

    doLast {
        assetsDir.mkdirs()

        def sdk = android.sdkDirectory
        def btVersion = android.buildToolsVersion
        def isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
        def d8Exe = new File(sdk, "build-tools/${btVersion}/d8${isWindows ? '.bat' : ''}")
        def androidJar = new File(sdk, "platforms/android-${android.compileSdk}/android.jar")

        def args = [d8Exe.absolutePath]
        if (androidJar.exists()) {
            args += ["--lib", androidJar.absolutePath]
        }
        args += ["--min-api", "${android.defaultConfig.minSdk}"]
        args += ["--output", outputJar.absolutePath]
        args += [inputJar.absolutePath]

        exec { commandLine args }
        println "Dexified: ${inputJar.name} -> ${outputJar.name}"
    }
}
preBuild.dependsOn dexifyPatchedJar

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    // Huawei CameraKit public SDK — used by HuaweiSDKEngine
    implementation files('libs/camerakit-1.1.3.aar')
    // HwCameraKit system APK JAR — compile-only for CustomSDKEngine API access.
    // At runtime, impl classes are loaded from the dexified asset via ParentLastDexClassLoader
    // to bypass Huawei's shared-library classloader that shadows our patched version.
    compileOnly files('../libs/hwcamerakit.jar')
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'com.github.mik3y:usb-serial-for-android:3.8.1'
}
